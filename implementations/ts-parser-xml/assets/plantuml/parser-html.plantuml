@startebnf
title EBNF - Basic HTML Parser


Document = [ Prolog ] , Element , EOF (* end of file *) ;

Prolog = XML_DECL_START (* <?xml *) , { Attributes } (* { version, encoding? } *) , SPECIAL_CLOSE (* ?> *) ;


Element = SpecialElement | VoidElement | AutoCloseElement | WithContentElement ;


SpecialElement = (
    "<" , "script" , { Attribute } , ">" , [ RAW_TEXT ] , "<" , "/" , "script" , ">" | 
    "<" , "style" , { Attribute } , ">" , RAW_TEXT , "<" , "/" , "style" , ">"
  ) 
  ;

VoidElement = (
    "<" , VOID_TAG_NAME (* for eg.: meta, link, img, input, br, hr, ... *) , { Attribute } , ">"
  )
  ;

AutoCloseElement = (
    "<" , NAME , { Attribute } , "/" , ">"
  )
  ;

WithContentElement = (
    "<" , NAME , { Attribute } , ">" , Content , "<" , "/" , NAME , ">"
  )
  ;


Attribute = NAME , [ "=" , STRING ] ;

Content = { ( 
    TEXT |
    Element
  ) }-
  ;

@endebnf